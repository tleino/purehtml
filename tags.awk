BEGIN {
	for (i = 0; i < 256; i++)
		ord[sprintf("%c",i)] = i;
	hashsz=1024
}
/^[a-zA-Z_]/ {
	flags = "";
	jump = 0;
	for (i = 1; i <= length($2); i++) {
		if (i != 1)
			flags = flags " | ";
		if (substr($2,i,1) ~ /o/)
			flags = flags "TAG_OPTIONAL_CLOSE";
		if (substr($2,i,1) ~ /e/)
			flags = flags "TAG_EMPTY";
		if (substr($2,i,1) ~ /s/)
			flags = flags "TAG_SPECIAL";
		if (substr($2,i,1) ~ /b/)
			flags = flags "TAG_BLOCK";
		if (substr($2,i,1) ~ /h/)
			flags = flags "TAG_HEADING";
		if (substr($2,i,1) ~ /f/)
			flags = flags "TAG_FORMAT";
	}
	if (length($2) == 0)
		flags = "0";

	addr = 0;
	key = $1;
	key = key "0";
	prime = 104729;
	modulo = 1048576;
	split($1, chars, "");
	for (i = 1; i <= length($1); i++) {
		c = ord[chars[i]];
		addr += c;
		addr *= prime;
		addr %= modulo;
	}
	addr %= hashsz;
	while (tag_arr[addr] != "") {
		addr++;
		jump++;
		addr %= hashsz;
	}
	if (flags == "")
		flags = 0;
	if (jump > 0)
		tag_arr[addr] = sprintf("\t{ \"%s\", %s } /* jump %d */,", $1, flags, jump);
	else
		tag_arr[addr] = sprintf("\t{ \"%s\", %s },", $1, flags);
	tag_names[addr] = toupper($1);
}
END {
	printf("/* generated by tags.awk */\n");
	if (mode == "c") {
		printf("static const struct tag tags[] = {\n");
		for (i = 0; i < hashsz; i++) {
			if (tag_arr[i] == "")
				printf("\t{ NULL, 0 }, /* %d */\n", i);
			else
				printf("%s /* %d */\n", tag_arr[i], i);
		}
		printf("};\n");
	} else {
		printf("enum tagid {\n");
		for (i = 0; i < hashsz; i++) {
			if (tag_names[i] != "")
				printf("\tTAG_%s=%d,\n", tag_names[i], i);
		}
		printf("};\n");
	}
}
